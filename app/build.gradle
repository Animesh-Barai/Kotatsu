apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def gitCommits = 'git rev-list --all --count'.execute([], rootDir).text.trim().toInteger()
def gitBranch = 'git branch --show-current'.execute([], rootDir).text.trim()

android {
	compileSdkVersion 29
	buildToolsVersion '29.0.3'

	defaultConfig {
		applicationId "org.koitharu.kotatsu"
		minSdkVersion 21
		targetSdkVersion 29
		versionCode gitCommits
		versionName "0.1"

		buildConfigField 'String', 'GIT_BRANCH', "\"${gitBranch}\""

		kapt {
			arguments {
				arg("room.schemaLocation", "$projectDir/schemas".toString())
			}
		}
	}
	archivesBaseName = "kotatsu_${gitCommits}"
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	kotlinOptions {
		jvmTarget = JavaVersion.VERSION_1_8.toString()
	}
	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}
	lintOptions {
		disable 'MissingTranslation'
		abortOnError false
	}
	testOptions {
		unitTests.includeAndroidResources = true
		unitTests.returnDefaultValues = true
	}
}
androidExtensions {
	experimental = true
}
dependencies {
	implementation fileTree(dir: "libs", include: ["*.jar"])
	implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3'
	implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.3'

	implementation 'androidx.core:core-ktx:1.3.0-alpha01'
	implementation "androidx.fragment:fragment-ktx:1.2.2"
	implementation 'androidx.appcompat:appcompat:1.2.0-alpha02'
	implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'
	implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0-alpha03'
	implementation 'androidx.recyclerview:recyclerview:1.2.0-alpha01'
	implementation 'androidx.preference:preference:1.1.0'
	implementation 'com.google.android.material:material:1.2.0-alpha05'

	implementation 'androidx.room:room-runtime:2.2.4'
	implementation 'androidx.room:room-ktx:2.2.4'
	kapt 'androidx.room:room-compiler:2.2.4'

	implementation 'com.github.moxy-community:moxy:2.1.1'
	implementation 'com.github.moxy-community:moxy-androidx:2.1.1'
	implementation 'com.github.moxy-community:moxy-material:2.1.1'
	implementation 'com.github.moxy-community:moxy-ktx:2.1.1'
	kapt 'com.github.moxy-community:moxy-compiler:2.1.1'

	implementation 'com.squareup.okhttp3:okhttp:4.4.0'
	implementation 'com.squareup.okio:okio:2.4.3'
	implementation 'org.jsoup:jsoup:1.12.2'

	implementation 'org.koin:koin-android:2.1.0'
	implementation 'io.coil-kt:coil:0.9.5'
	implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
	implementation 'com.tomclaw.cache:cache:1.0'

	debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.2'

	testImplementation 'junit:junit:4.13'
	testImplementation 'androidx.test:core:1.2.0'
	testImplementation 'org.mockito:mockito-core:2.23.0'
}